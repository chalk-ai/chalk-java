plugins {
    id "maven-publish"
    id "java-library"
    id "io.freefair.lombok" version "8.3"
    id "signing"
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

group 'ai.chalk'
version '0.8.0'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    api 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.2'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    api 'com.google.protobuf:protobuf-java:3.25.1'
    api 'io.grpc:grpc-core:1.64.0'
    api 'io.grpc:grpc-protobuf:1.64.0'
    api 'io.grpc:grpc-stub:1.64.0'
    api 'org.apache.arrow:arrow-compression:13.0.0'
    api 'org.apache.arrow:arrow-memory-netty:13.0.0'
    api 'org.apache.arrow:arrow-vector:13.0.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
}

test {
    useJUnitPlatform()
    jvmArgs '--add-opens=java.base/java.nio=ALL-UNNAMED'
    testLogging {
        // Ensure that the standard output from the tests is shown in CI logs
        showStandardStreams = true
        events "passed", "skipped", "failed"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Required for OSSRH publishing
task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

// Required for OSSRH publishing
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

def configurePom = { pom ->
    pom.name = 'Chalk Java'
    pom.description = 'The Java client for Chalk'
    pom.url = 'https://github.com/chalk-ai/chalk-java'

    pom.licenses {
        license {
            name = 'The Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    pom.developers {
        developer {
            id = 'chalk-ai'
            name = 'Chalk'
            email = 'support@chalk.ai'
        }
    }

    pom.scm {
        connection = 'scm:git:git://github.com/chalk-ai/chalk-java.git'
        developerConnection = 'scm:git:ssh://github.com:chalk-ai/chalk-java.git'
        url = 'https://github.com/chalk-ai/chalk-java/'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom.with configurePom
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'

            credentials {
                username = System.getenv("SONATYPE_USERNAME") ?: project.findProperty("sonatypeUsername")
                password = System.getenv("SONATYPE_PASSWORD") ?: project.findProperty("sonatypePassword")
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("SONATYPE_USERNAME") ?: project.findProperty("sonatypeUsername")
            password = System.getenv("SONATYPE_PASSWORD") ?: project.findProperty("sonatypePassword")
        }
    }
}

signing {
    def signingKey = System.getenv("SIGNING_PRIVATE_KEY") ?: project.findProperty("signingPrivateKey")
    def signingPassword = System.getenv("SIGNING_PASSWORD") ?: project.findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}
